use std::str::FromStr;

use crate::ast::*;

grammar;

match {
    "+",
    "-",
    "*",
    "/",
    "%",
    "=",
    "!=",
    ">",
    "<",
    ">=",
    "<=",
    ",",
    "T",
    r"[0-9]+",
    r"[a-z]+",
    "[",
    "]",
    "(",
    ")",
    ":=",
    "IF",
    "THEN",
    "ELSE",
    "ENDIF",
    "WHILE",
    "DO",
    "ENDWHILE",
    "REPEAT",
    "UNTIL",
    ";",
    "READ",
    "WRITE",
    "PROGRAM",
    "IS",
    "IN",
    "END",
    "PROCEDURE",
    "\n" => {},
    r"\s*" => {}, // Skip whitespace
    r"#[^\n\r]*[\n\r]*" => {}, // Skip `# comment`
}

pub Program: Program = {
    <procedures:Procedures> <main:Main> => Program::WithProcedures(procedures, main),
    <main:Main> => Program::WithOutProcedures(main),
};

Procedures: Procedures = {
    <mut procedures:Procedures> "PROCEDURE" <procedure_head:ProcedureHead> "IS" <declarations:Declarations> "IN" <commands:Commands> "END" => {
        procedures.push(Procedure::WithDeclarations(procedure_head, declarations, commands));
        procedures
    },
    <mut procedures:Procedures> "PROCEDURE" <procedure_head:ProcedureHead> "IS" "IN" <commands:Commands> "END" => {
        procedures.push(Procedure::WithOutDeclarations(procedure_head, commands));
        procedures
    },
    "PROCEDURE" <procedure_head:ProcedureHead> "IS" <declarations:Declarations> "IN" <commands:Commands> "END" => vec![Procedure::WithDeclarations(procedure_head, declarations, commands)],
    "PROCEDURE" <procedure_head:ProcedureHead> "IS" "IN" <commands:Commands> "END" => vec![Procedure::WithOutDeclarations(procedure_head, commands)],
};

Main: Main = {
    "PROGRAM" "IS" <declarations:Declarations> "IN" <commands:Commands> "END" => Main::WithDeclarations(declarations, commands),
    "PROGRAM" "IS" "IN" <commands:Commands> "END" => Main::WithOutDeclarations(commands),
};

Commands: Commands = {
    <mut commands:Commands> <command:Command> => {
        commands.push(command);
        commands
    },
    <command: Command> => vec![command],
};

Command: Command = {
    <id:Identifier> ":=" <expression:Expression> ";" => Command::Assign(id, expression),
    "IF" <condition:Condition> "THEN" <commands0:Commands> "ELSE" <commands1:Commands> "ENDIF" => Command::IfElse(condition, commands0, commands1),
    "IF" <condition:Condition> "THEN" <commands:Commands> "ENDIF" => Command::If(condition, commands),
    "WHILE" <condition:Condition> "DO" <commands:Commands> "ENDWHILE" => Command::While(condition, commands),
    "REPEAT" <commands:Commands> "UNTIL" <condition:Condition> ";" => Command::Repeat(commands, condition),
    <procedure_call:ProcedureCall> ";" => Command::ProcCall(procedure_call),
    "READ" <id:Identifier> ";" => Command::Read(id),
    "WRITE" <value:Value> ";" => Command::Write(value),
};

ProcedureHead: ProcedureHead = {
    <p:Pidentifier> "(" <a: ArgumentsDeclaration> ")" => (p, a),
};

ProcedureCall: ProcedureCall = {
    <p:Pidentifier> "(" <a: Arguments> ")" => (p, a),
};


Declarations: Declarations = {
    <mut v:Declarations> "," <p: Pidentifier> => {
        v.push(DeclarationVariant::Base(p));
        v
    },
    <mut v:Declarations> "," <p: Pidentifier> "[" <n:Num> "]" => {
        v.push(DeclarationVariant::NumIndexed(p, n));
        v
    },
    <p: Pidentifier> => vec![DeclarationVariant::Base(p)],
    <p: Pidentifier> "[" <n:Num> "]" => vec![DeclarationVariant::NumIndexed(p, n)],
};

ArgumentsDeclaration: ArgumentsDeclaration = {
    <mut v:ArgumentsDeclaration> "," <p: Pidentifier> => {
        v.push(ArgumentsDeclarationVariant::Base(p));
        v
    },
    <mut v:ArgumentsDeclaration> "," "T" <p: Pidentifier> => {
        v.push(ArgumentsDeclarationVariant::Table(p));
        v
    },
    "T" <p: Pidentifier> => vec![ArgumentsDeclarationVariant::Table(p)],
    <p: Pidentifier> => vec![ArgumentsDeclarationVariant::Base(p)],
};

Arguments: Arguments = {
    <mut v:(<Pidentifier> ",")*> <e:Pidentifier?> => match e {
        None=> v,
        Some(e) => {
            v.push(e);
            v
        }
    }, 
};

Expression: Expression = {
    <v:Value> => Expression::Val(v),
    <v0:Value> "+" <v1:Value>=> Expression::Add(v0, v1),
    <v0:Value> "-" <v1:Value>=> Expression::Substract(v0, v1),
    <v0:Value> "*" <v1:Value>=> Expression::Multiply(v0, v1),
    <v0:Value> "/" <v1:Value>=> Expression::Divide(v0, v1),
    <v0:Value> "%" <v1:Value>=> Expression::Modulo(v0, v1),
};

Condition: Condition = {
    <v0:Value> "=" <v1:Value> => Condition::Equal(v0, v1),
    <v0:Value> "!=" <v1:Value> => Condition::NotEqual(v0, v1),
    <v0:Value> ">" <v1:Value> => Condition::Greater(v0, v1),
    <v0:Value> "<" <v1:Value> => Condition::Lower(v0, v1),
    <v0:Value> ">=" <v1:Value> => Condition::GreaterOrEqual(v0, v1),
    <v0:Value> "<=" <v1:Value> => Condition::LowerOrEqual(v0, v1),
};

Value: Value = {
    <n:Num> => Value::Num(n),
    <id:Identifier> => Value::Id(id),
};

Identifier: Identifier = {
    <p: Pidentifier> => Identifier::Base(p),
    <p: Pidentifier> "[" <n: Num> "]" => Identifier::NumIndexed(p, n),
    <p0: Pidentifier> "[" <p1: Pidentifier> "]" => Identifier::PidIndexed(p0, p1),
};

Num: Num = <s:r"[0-9]+"> => u64::from_str(s).unwrap();

Pidentifier: Pidentifier = <s:r"[a-z]+"> => s.to_string();
